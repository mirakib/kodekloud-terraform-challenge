# Associate Elastic IP with EC2 Instance Using Terraform

The Nautilus DevOps Team has received a new request from the Development Team to set up a new EC2 instance. This instance will be used to host a new application that requires a stable IP address. To ensure that the instance has a consistent public IP, an Elastic IP address needs to be associated with it. This setup will help the Development Team to have a reliable and consistent access point for their application.

1. Create an EC2 instance named `datacenter-ec2` using any Linux AMI like `Ubuntu`.

2. Instance type must be `t2.micro` and associate an Elastic IP address named `datacenter-eipwith` this instance.

3. Use the `main.tf` file (do not create a separate `.tf` file) to provision the `EC2-Instance` and `Elastic IP`.

4. Use the `outputs.tf` file and output the instance name using variable `KKE_instance_name` and the Elastic IP using variable `KKE_eip`.

5. Before submitting the task, ensure that `terraform plan` returns `No changes. Your infrastructure matches the configuration`.

**`/home/bob/terraform/main.tf`**

```hcl
data "aws_ami" "ubuntu" {
  most_recent = true
  owners      = ["099720109477"] # Canonical

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-*-amd64-server-*"]
  }

  filter {
    name   = "root-device-type"
    values = ["ebs"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }
}

# Create EC2 Instance
resource "aws_instance" "datacenter_ec2" {
  ami           = data.aws_ami.ubuntu.id
  instance_type = "t2.micro"

  tags = {
    Name = "datacenter-ec2"
  }
}

# Allocate and Associate Elastic IP
resource "aws_eip" "datacenter_eip" {
  domain   = "vpc"
  instance = aws_instance.datacenter_ec2.id

  tags = {
    Name = "datacenter-eip"
  }
}
```

**`/home/bob/terraform/outputs.tf`**

```hcl
output "KKE_instance_name" {
  value = aws_instance.datacenter_ec2.tags["Name"]
}

output "KKE_eip" {
  value = aws_eip.datacenter_eip.public_ip
}
```

## How to apply

```bash
terraform init
terraform apply -auto-approve
terraform plan
```
