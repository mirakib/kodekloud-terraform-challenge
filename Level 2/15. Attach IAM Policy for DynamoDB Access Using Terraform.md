# Attach IAM Policy for DynamoDB Access Using Terraform

The DevOps team has been tasked with creating a secure DynamoDB table and enforcing fine-grained access control using IAM. This setup will allow secure and restricted access to the table from trusted AWS services only.

As a member of the Nautilus DevOps Team, your task is to perform the following using Terraform:

1. Create a DynamoDB Table: Create a table named `devops-table` with minimal configuration.
2. Create an IAM Role: Create an IAM role named `devops-role` that will be allowed to access the table.
3. Create an IAM Policy: Create a policy named `devops-readonly-policy` that should grant read-only access (GetItem, Scan, Query) to the specific DynamoDB table and attach it to the role.
4. Create the `main.tf` file (do not create a separate `.tf` file) to provision the table, role, and policy.
5. Create the `variables.tf` file with the following variables:
   - `KKE_TABLE_NAME`: name of the DynamoDB table
   - `KKE_ROLE_NAME`: name of the IAM role
   - `KKE_POLICY_NAME`: name of the IAM policy

6. Create the outputs.tf file with the following outputs:
   - `kke_dynamodb_table`: name of the DynamoDB table
   - `kke_iam_role_name`: name of the IAM role
   - `kke_iam_policy_name`: name of the IAM policy

7. Define the actual values for these variables in the `terraform.tfvars` file.
8. Ensure that the IAM policy allows only read access and restricts it to the specific DynamoDB table created.

**`/home/bob/terraform/main.tf`**

```hcl
resource "aws_dynamodb_table" "kke_table" {
  name         = var.KKE_TABLE_NAME
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "id"

  attribute {
    name = "id"
    type = "S"
  }

  tags = {
    Name = var.KKE_TABLE_NAME
  }
}

resource "aws_iam_role" "kke_role" {
  name = var.KKE_ROLE_NAME

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Principal = {
          Service = "ec2.amazonaws.com"
        }
        Action = "sts:AssumeRole"
      }
    ]
  })
}

resource "aws_iam_policy" "kke_policy" {
  name = var.KKE_POLICY_NAME

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "dynamodb:GetItem",
          "dynamodb:Scan",
          "dynamodb:Query"
        ]
        Resource = aws_dynamodb_table.kke_table.arn
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "kke_attach" {
  role       = aws_iam_role.kke_role.name
  policy_arn = aws_iam_policy.kke_policy.arn
}
```

**`/home/bob/terraform/variables.tf`**

```hcl
variable "KKE_TABLE_NAME" {
  description = "Name of the DynamoDB table"
  type        = string
}

variable "KKE_ROLE_NAME" {
  description = "Name of the IAM Role"
  type        = string
}

variable "KKE_POLICY_NAME" {
  description = "Name of the IAM Policy"
  type        = string
}
```

**`/home/bob/terraform/outputs.tf`**

```hcl
output "kke_dynamodb_table" {
  value = aws_dynamodb_table.kke_table.name
}

output "kke_iam_role_name" {
  value = aws_iam_role.kke_role.name
}

output "kke_iam_policy_name" {
  value = aws_iam_policy.kke_policy.name
}
```

**`/home/bob/terraform/terraform.tfvars`**

```hcl
KKE_TABLE_NAME  = "devops-table"
KKE_ROLE_NAME   = "devops-role"
KKE_POLICY_NAME = "devops-readonly-policy"
```

## How to apply

```bash
terraform init
terraform apply -auto-approve
terraform plan
```
