# Grant EC2 Access to S3 Bucket Using Terraform

The Nautilus DevOps team wants to set up EC2 instances that securely upload application logs to S3 using IAM roles.

1. Create an EC2 instance named `xfusion-ec2` that can access an S3 bucket securely.

2. Create an S3 bucket named `xfusion-logs-30861`.

3. Create an IAM role named `xfusion-role` with a policy named `xfusion-access-policy` allowing `S3 PutObject` on the above bucket.

4. Attach the IAM role to the EC2 instance to allow it to upload logs to the bucket.

5. Create the `main.tf` (do not create a separate `.tf` file) to provision the EC2, s3, role and policy.

6. Create the variables.tffile to declare the following:
   - `KKE_BUCKET_NAME`: name of the bucket.
   - `KKE_POLICY_NAME`: name of the policy.
   - `KKE_ROLE_NAME`: name of the role.

7. Create the `terraform.tfvars` file to assign values to variables.

8. Create a `data.tf` file to fetch the latest `Amazon Linux 2` AMI.


**`/home/bob/terraform/main.tf`**

```hcl
resource "aws_s3_bucket" "xfusion_bucket" {
  bucket = var.KKE_BUCKET_NAME
}

resource "aws_iam_policy" "xfusion_policy" {
  name        = var.KKE_POLICY_NAME
  description = "Allow EC2 to upload logs to S3 bucket"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "s3:PutObject"
        ]
        Resource = "${aws_s3_bucket.xfusion_bucket.arn}/*"
      }
    ]
  })
}

resource "aws_iam_role" "xfusion_role" {
  name = var.KKE_ROLE_NAME

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Principal = {
          Service = "ec2.amazonaws.com"
        }
        Action = "sts:AssumeRole"
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "xfusion_attach" {
  role       = aws_iam_role.xfusion_role.name
  policy_arn = aws_iam_policy.xfusion_policy.arn
}

resource "aws_iam_instance_profile" "xfusion_profile" {
  name = "xfusion-instance-profile"
  role = aws_iam_role.xfusion_role.name
}

resource "aws_instance" "xfusion_ec2" {
  ami                    = data.aws_ami.amazon_linux.id
  instance_type          = "t2.micro"
  iam_instance_profile   = aws_iam_instance_profile.xfusion_profile.name

  tags = {
    Name = "xfusion-ec2"
  }
}
```

**`/home/bob/terraform/variables.tf`**

```hcl
variable "KKE_BUCKET_NAME" {
  description = "Name of the S3 bucket"
  type        = string
}

variable "KKE_POLICY_NAME" {
  description = "Name of the IAM policy"
  type        = string
}

variable "KKE_ROLE_NAME" {
  description = "Name of the IAM role"
  type        = string
}
```


**`/home/bob/terraform/terraform.tfvars`**

```hcl
KKE_BUCKET_NAME = "xfusion-logs-30861"
KKE_POLICY_NAME = "xfusion-access-policy"
KKE_ROLE_NAME   = "xfusion-role"
```


**`/home/bob/terraform/data.tf`**

```hcl
data "aws_ami" "amazon_linux" {
  most_recent = true
  owners      = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-*-x86_64-gp2"]
  }
}
```

## How to apply

```bash
terraform init
terraform apply -auto-approve
terraform plan
```
