# Integrate SNS with SQS for Messaging Using Terraform

The Nautilus DevOps team is implementing a messaging system in AWS. They want to create an SNS topic and an SQS queue. The team needs to subscribe the SQS queue to the SNS topic so that any messages sent to the SNS topic will be delivered to the SQS queue.

1. Create an SNS topic named `devops-sns-topic`.

2. Create an SQS queue named `devops-sqs-queue`.

3. Subscribe the SQS queue to the SNS topic.

4. Use the `main.tf` file (do not create a separate `.tf` file) to provision the SNS topic and SQS queue.

5. Create the outputs.tf file, and use the following:

   - The ARN of the SNS topic using the output variable `kke_sns_topic_arn`.

   - The URL of the SQS queue using the output variable `kke_sqs_queue_url`.

**`/home/bob/terraform/main.tf`**

```hcl
resource "aws_sns_topic" "devops_sns_topic" {
  name = "devops-sns-topic"
}

resource "aws_sqs_queue" "devops_sqs_queue" {
  name = "devops-sqs-queue"
}

resource "aws_sqs_queue_policy" "devops_sqs_policy" {
  queue_url = aws_sqs_queue.devops_sqs_queue.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "Allow-SNS-SendMessage"
        Effect = "Allow"
        Principal = {
          Service = "sns.amazonaws.com"
        }
        Action   = "sqs:SendMessage"
        Resource = aws_sqs_queue.devops_sqs_queue.arn
        Condition = {
          ArnEquals = {
            "aws:SourceArn" = aws_sns_topic.devops_sns_topic.arn
          }
        }
      }
    ]
  })
}

resource "aws_sns_topic_subscription" "devops_subscription" {
  topic_arn = aws_sns_topic.devops_sns_topic.arn
  protocol  = "sqs"
  endpoint  = aws_sqs_queue.devops_sqs_queue.arn
}
```

**`/home/bob/terraform/outputs.tf`**

```hcl
output "kke_sns_topic_arn" {
  value = aws_sns_topic.devops_sns_topic.arn
}

output "kke_sqs_queue_url" {
  value = aws_sqs_queue.devops_sqs_queue.id
}
```

## How to apply

```bash
terraform init
terraform apply -auto-approve
terraform plan
```
