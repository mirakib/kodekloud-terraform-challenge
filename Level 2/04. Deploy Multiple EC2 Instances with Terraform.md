# Deploy Multiple EC2 Instances with Terraform

The Nautilus DevOps team wants to provision multiple EC2 instances in AWS using Terraform. Each instance should follow a consistent naming convention and be deployed using a modular and scalable setup.

Use Terraform to:

1. Create `3` EC2 instances using the `count` parameter.

2. Name each EC2 instance with the prefix `devops-instance` (e.g., `devops-instance-1`).

3. Instances should be `t2.micro`.

4. The key named should be `devops-key`.

5. Create `main.tf` file (do not create a separate `.tf` file) to provision these instances.

6. Use `variables.tf` file with the following:
   - `KKE_INSTANCE_COUNT`: number of instances.
   - `KKE_INSTANCE_TYPE`: type of the instance.
   - `KKE_KEY_NAME`: name of key used.
   - `KKE_INSTANCE_PREFIX`: name of the instnace.

7. Use the `locals.tf` file to define a local variable named `AMI_ID` that retrieves the latest `Amazon Linux 2` AMI using a data source.

8. Use `terraform.tfvars` to assign values to the variables.

9. Use outputs.tf file to output the following:
   - `kke_instance_names`: names of the instances created.

10. Before submitting the task, ensure that `terraform plan` returns `No changes. Your infrastructure matches the configuration`.

**`/home/bob/terraform/main.tf`**

```hcl
resource "aws_instance" "kke_instances" {
  count         = var.KKE_INSTANCE_COUNT
  ami           = local.AMI_ID
  instance_type = var.KKE_INSTANCE_TYPE
  key_name      = var.KKE_KEY_NAME

  tags = {
    Name = "${var.KKE_INSTANCE_PREFIX}-${count.index + 1}"
  }
}
```

**`/home/bob/terraform/variables.tf`**

```hcl
variable "KKE_INSTANCE_COUNT" {
  description = "Number of EC2 instances to create"
  type        = number
}

variable "KKE_INSTANCE_TYPE" {
  description = "EC2 instance type"
  type        = string
}

variable "KKE_KEY_NAME" {
  description = "Key pair name"
  type        = string
}

variable "KKE_INSTANCE_PREFIX" {
  description = "Prefix for EC2 instance names"
  type        = string
}
```

**`/home/bob/terraform/locals.tf`**

```hcl
data "aws_ami" "amazon_linux_2" {
  most_recent = true

  owners = ["amazon"]

  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-*-x86_64-gp2"]
  }
}

locals {
  AMI_ID = data.aws_ami.amazon_linux_2.id
}
```

**`/home/bob/terraform/terraform.tfvars`**

```hcl
KKE_INSTANCE_COUNT  = 3
KKE_INSTANCE_TYPE   = "t2.micro"
KKE_KEY_NAME        = "devops-key"
KKE_INSTANCE_PREFIX = "devops-instance"
```


**`/home/bob/terraform/outputs.tf`**

```hcl
output "kke_instance_names" {
  description = "Names of created EC2 instances"
  value = [
    for i in aws_instance.kke_instances :
    i.tags["Name"]
  ]
}
```

## How to apply

```bash
terraform init
terraform apply -auto-approve
terraform plan
```
